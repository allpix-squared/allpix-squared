# SPDX-FileCopyrightText: 2019-2024 CERN and the Allpix Squared authors
# SPDX-License-Identifier: MIT

@PACKAGE_INIT@

SET_AND_CHECK(ALLPIX_INCLUDE_DIR "@PACKAGE_ALLPIX_INCLUDE_DIR@")
SET_AND_CHECK(ALLPIX_LIBRARY_DIR "@PACKAGE_ALLPIX_LIBRARY_DIR@")

# Force same CXX standard for downstream project
SET(CMAKE_CXX_STANDARD @CMAKE_CXX_STANDARD@)
SET(CMAKE_CXX_STANDARD_REQUIRED @CMAKE_CXX_STANDARD_REQUIRED@)
SET(CMAKE_CXX_EXTENSIONS @CMAKE_CXX_EXTENSIONS@)

# Require dependencies for inclusion in downstream project:
INCLUDE(CMakeFindDependencyMacro)
FIND_DEPENDENCY(Threads)
FIND_DEPENDENCY(Boost 1.64.0 COMPONENTS random)
FIND_DEPENDENCY(ROOT COMPONENTS Geom Core GenVector Hist RIO NO_MODULE)
FIND_DEPENDENCY(PkgConfig)
PKG_CHECK_MODULES(Eigen3 QUIET IMPORTED_TARGET eigen3)

# Add targets and configure for external module generation
INCLUDE("${CMAKE_CURRENT_LIST_DIR}/AllpixConfigTargets.cmake")

SET(ALLPIX_MODULE_EXTERNAL TRUE)
INCLUDE("${CMAKE_CURRENT_LIST_DIR}/AllpixMacros.cmake")

# Prepare ROOT Targets if necessary:
ALLPIX_SETUP_ROOT_TARGETS()

# Check for presence of required components:
foreach(_comp ${Allpix_FIND_COMPONENTS})
  find_library(Allpix_${_comp}_LIBRARY ${_comp} HINTS ${ALLPIX_LIBRARY_DIR})
  if(Allpix_${_comp}_LIBRARY)
    mark_as_advanced(Allpix_${_comp}_LIBRARY)
    list(APPEND Allpix_LIBRARIES ${Allpix_${_comp}_LIBRARY})
    SET(Allpix_${_comp}_FOUND TRUE)
  endif()
endforeach()
if(Allpix_LIBRARIES)
  list(REMOVE_DUPLICATES Allpix_LIBRARIES)
endif()

CHECK_REQUIRED_COMPONENTS(Allpix)
